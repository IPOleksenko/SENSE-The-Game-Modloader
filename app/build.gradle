plugins {
    id 'com.android.application'
}

def getCommitCount() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim().toInteger()
    } catch (Exception e) {
        println "Error: ${e.message}"
        e.printStackTrace()
        return 1
    }
}
def commitCount = getCommitCount()

android {
    compileSdk 34
    ndkVersion '27.0.12077973'

    defaultConfig {
        applicationId = 'com.ipoleksenko.sense.modloader'
        minSdkVersion 21
        targetSdkVersion 35

        versionCode commitCount
        versionName commitCount.toString()

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static',
                        '-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON'
                cppFlags '-frtti -fexceptions'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def fileName = variant.buildType.name == 'release' ?
                    "SENSE-The-Game-Modloader.apk" :
                    "SENSE-The-Game-Modloader-DEBUG-${variant.versionName}.apk"
            output.outputFileName = fileName
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/CMakeLists.txt'
        }
    }
    namespace 'com.ipoleksenko.sense.modloader'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.getkeepsafe.relinker:relinker:1.4.4'
}
